plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.10'  //requires JDK11+
    id 'com.gluonhq.client-gradle-plugin' version '0.1.40'  //requires GraalVM 21.1+
}

wrapper {
    gradleVersion = "7.0.2"
    distributionType = Wrapper.DistributionType.ALL
}

group = 'com.github.chirontt'
description = "JGit HTTP server's native executable built by GraalVM"

ext {
    mainClassName = 'com.github.chirontt.gitserver.JGitHttpServer'
    jgitReleaseVersion = '5.11.1.202105131744-r'
    jettyVersion = '9.4.40.v20210413'
    slf4jVersion = '1.7.30'
}

repositories {
    mavenCentral()
    mavenLocal()
}

compileJava {
    options.release = 11  //use JDK11+ for compiling & running
    options.encoding = 'UTF-8'
}

dependencies {
    implementation "org.eclipse.jgit:org.eclipse.jgit.http.server:$jgitReleaseVersion"
    implementation "org.eclipse.jetty:jetty-servlet:$jettyVersion"
    implementation "org.slf4j:slf4j-simple:$slf4jVersion"
}

application {
    mainClass = project.mainClassName
    applicationName = project.name
}

gluonClient {
    def buildTimeInitClasses = file('src/main/resources/META-INF/substrate/config/initbuildtime').getText('UTF-8').trim()
    def runTimeInitClasses = [
        'org.eclipse.jgit.internal.storage.file.WindowCache',
        'org.eclipse.jgit.lib.internal.WorkQueue',
        'org.eclipse.jgit.lib.RepositoryCache',
        'org.eclipse.jgit.transport.HttpAuthMethod',
        'org.eclipse.jgit.util.FileUtils',
    ]

    compilerArgs = [
        '--initialize-at-build-time=' + buildTimeInitClasses,
        '--initialize-at-run-time=' + runTimeInitClasses.join(','),
    ]
}

